plugins {
    id 'java-library'
    id 'java'
    id 'com.modrinth.minotaur' version "2.8.10"
}

group 'org.geysermc.extension.connect'
version '1.0.0'

repositories {
//    mavenLocal()
    mavenCentral()
    maven {
        url 'https://repo.opencollab.dev/main'
    }
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots'
        mavenContent {
            snapshotsOnly()
        }
    }
    maven {
        url 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
    }
    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    compileOnly(libs.bundles.geyser) {
        exclude group: 'io.netty'
        exclude group: 'io.netty.incubator'
    }
    compileOnly libs.yamlConfigurate

    implementation libs.sqlite
    implementation libs.mysql
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    archiveFileName = "${project.name}.jar"

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

test {
    useJUnitPlatform()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

tasks.getByName("modrinth") {
    dependsOn("modrinthSyncBody")
}

modrinth {
    token = System.getenv("MODRINTH_TOKEN") ?: ""
    debugMode = (System.getenv("MODRINTH_TOKEN") == null)
    projectId = "geyserconnect"
    def versionDisplay = "${project.version}-b${System.getenv("BUILD_NUMBER")}"
    versionName = "GeyserConnect ${versionDisplay}"
    versionNumber = versionDisplay
    versionType = "release"
    changelog = System.getenv("CHANGELOG") ?: ""
    gameVersions.addAll(["1.21.10"])
    loaders.addAll(["geyser"])
    failSilently = true

    syncBodyFrom = file("README.md").text
    uploadFile.set(tasks.getByPath("jar"))
}

